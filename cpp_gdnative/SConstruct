#!python
import os

target = ARGUMENTS.get("target", "debug")
platform = ARGUMENTS.get("platform", "windows")
bits = ARGUMENTS.get("bits", 64)

final_lib_path = 'bin/'

# This makes sure to keep the session environment variables on windows, 
# that way you can run scons in a vs 2017 prompt and it will find all the required tools
env = Environment()
if platform == "windows":
    env = Environment(ENV = os.environ)

def add_sources(sources, directory):
    for file in os.listdir(directory):
        if file.endswith('.cpp') or file.endswith('.cc'):
            sources.append(directory + '/' + file)

if platform == "osx":
    env.Append(CCFLAGS = ['-g','-Os', '-arch', 'x86_64', '-std=c++17'])
    env.Append(LINKFLAGS = ['-arch', 'x86_64'])

    final_lib_path += 'osx/'

elif platform == "linux":
    env.Append(CCFLAGS = ['-fPIC', '-g','-Os', '-std=c++17'])

    final_lib_path += 'x11/'

elif platform == "windows":
    if target == "debug":
        env.Append(CCFLAGS = ['-EHsc', '-D_DEBUG', '-MDd'])
    else:
        env.Append(CCFLAGS = ['-O2', '-EHsc', '-DNDEBUG', '-MD'])

    final_lib_path = final_lib_path + 'win' + str(bits) + '/'

env.Append(CPPPATH=['.', '../cpp', '../cpp/fdeep-include', '../cpp/fplus-include', 'src/', 'godot-cpp/include', 'godot-cpp/gdextension', 'godot-cpp/gen/include'])
env.Append(LIBPATH="godot-cpp/bin")
env.Append(LIBS=["godot-cpp.%s.%s.%s" % (platform, target, bits)])

sources = []
add_sources(sources, "src")
add_sources(sources, '../cpp')
sources.append('../server/protocol.cc')
sources.append('../server/versioning.cc')

library = env.SharedLibrary(target=final_lib_path + 'libadrestia', source=sources)
Default(library)
